// Load plugins
buildscript {
    repositories {
      maven { url "https://jcenter.bintray.com" }
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.2'
    }
}
apply plugin: 'java'
apply plugin: 'de.undercouch.download'

// Set version information
version = '3.1.0'
// will generate a warning with JDK 8, since the runtime jar (rt.jar) of
// Java 7 is not available. No optimal solution here.
sourceCompatibility = 1.7
targetCompatibility = 1.7

// Choose Xenon 1.0.0 (from repo) or put a xenon.jar or xenon-[version].jar in lib/ and set that version here
def xenonVersion = '1.0.0'

//Get dependencies locally and then from Maven central repository
repositories {
  flatDir { dirs 'lib' }
  maven { url "http://nlesc.github.io/Xenon/repo" }
  mavenCentral()
}

//Project dependencies
dependencies {
  // state all packages that are explicitly imported in Osmium here,
  // even if there already is a transitive dependency
  compile group: 'com.yammer.dropwizard', name: 'dropwizard-core', version: '0.6.2', transitive: true
  compile group: 'com.yammer.dropwizard', name: 'dropwizard-client', version: '0.6.2', transitive: true
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.1.4'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.4'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.1.4'
  compile group: 'javax.validation', name: 'validation-api', version: '1.0.0.GA'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
  compile group: 'commons-codec', name: 'commons-codec', version: '1.6'
  compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.2.5', transitive: true

  // Xenon dependencies. downloadJGlobus task (below) downloads xenon/lib/**/*.jar to ./lib/
  compile group: 'nl.esciencecenter', name: 'xenon', version: xenonVersion
  compile group: 'com.jcraft', name: 'jsch', version: '0.1.50'  
  compile group: 'commons-net', name: 'commons-net', version: '3.3'
  compile group: 'com.google.code.findbugs', name: 'findbugs', version: '2.0.2'
  compile module(group: 'org.jglobus', name: 'cog-jglobus', version: '1.8.0') {
    dependency group: 'org.jglobus', name: 'cog-jobmanager', version: '1.8.0'
    dependency group: 'org.jglobus', name: 'cog-url', version: '1.8.0'
    dependency group: 'commons-logging', name: 'commons-logging', version: '1.1'
    dependency group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3', transitive: true
    dependency group: 'cryptix', name: 'cryptix-asn1'
    dependency group: 'cryptix', name: 'cryptix'
    dependency group: 'cryptix', name: 'cryptix32'
    dependency group: 'org.bountycastle', name: 'jce-jdk13', version: '131'
    dependency group: 'org.ietf', name: 'jgss'
    dependency group: 'com.claymoresystems', name: 'puretls', version: '0.9b4'
  }

  // Xenon runtime dependencies
  runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.0.11'
  runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.11'

  // Testing dependencies
  testCompile group: 'com.yammer.dropwizard', name: 'dropwizard-testing', version: '0.6.2', transitive: true
  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
  testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
  testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.5.2', transitive: true
  testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.5.2', transitive: true
  testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.2.5', classifier: 'tests'
}

jar {
  manifest {
    attributes(
      'Implementation-Title': 'Osmium',
      'Implementation-Version': version,
      'Main-Class': 'nl.esciencecenter.osmium.JobLauncherService',
    )
  }
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
  manifest {
    attributes(
      'Implementation-Title': 'Osmium',
      'Implementation-Version': version,
      'Main-Class': 'nl.esciencecenter.osmium.JobLauncherService',
    )
  }
  classifier = 'all'
  from {
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
  } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
  }
  with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives fatJar
}

// Globus dependencies are non-standard but already uploaded to the Xenon
// GitHub repository. This resolution is set to a fixed commit in the
// Xenon develop branch, to prevent expiration.
task downloadJGlobus(type: de.undercouch.gradle.tasks.download.Download) {
  def baseUrl = 'https://raw.github.com/NLeSC/Xenon/de9892dd92358bf69521819adfee15dc43e41ff4/lib/cog-jglobus/'
  src([
      baseUrl + 'cog-jglobus-1.8.0.jar',
      baseUrl + 'cog-jobmanager-1.8.0.jar',
      baseUrl + 'cog-url-1.8.0.jar',
      baseUrl + 'cryptix-asn1.jar',
      baseUrl + 'cryptix.jar',
      baseUrl + 'cryptix32.jar',
      baseUrl + 'jce-jdk13-131.jar',
      baseUrl + 'jgss.jar',
      baseUrl + 'puretls.jar',
    ])
  dest 'lib'
  overwrite false
}

compileJava.dependsOn downloadJGlobus
